***
*** ACTUALIZA LAS ACTIVIDADES
***
SELECT MTACT
IF ORDER()="MTACTXS"
   STORE "< ACTIVIDADES DE SECCION: "+ALLTRIM(MTSEC.DESSEC)+" >"                      TO WTITLE
   STORE "WDESPLA=FUNDESP():H='Plan', DESACT1:H='Actividad', WDESTIP=FUNDEST():H='Tipo'"    TO WCAMPOS
ELSE
   STORE "< ACTIVIDADES DE PLAN: "+ALLTRIM(MTPLA.DESPLA)+" >"                         TO WTITLE
   STORE "WDESSEC=FUNDESS():H='Seccion', DESACT1:H='Actividad', WDESTIP=FUNDEST():H='Tipo'" TO WCAMPOS
ENDIF
PUSH KEY CLEAR
DEFI WIND WINDACT FROM 04,00 TO 20,79;
          TITLE WTITLE ;
          FOOTER " F1=Inc, F2=Mod, F3=Eli, Enter=Sal " ;
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTI WIND WINDACT
ON KEY LABEL F1  DO PROACTI
ON KEY LABEL F2  DO PROACTM
ON KEY LABEL F3  DO PROACTE
ON KEY LABEL ENTER KEYBOARD '{CTRL+W}'

SELECT MTACT
BROWSE FIELDS &WCAMPOS;
       NOAPPEND NODELETE NOEDIT NOMENU NOOPTIMIZE REST SAVE IN WINDOW WINDACT;
       KEY    &WKEYACT
DEACT WIND WINDACT
POP KEY
RETURN

************
PROC PROACTI
************
PUSH KEY CLEAR
STORE "INCLUIR" TO WFLAGRACT
SELECT MTACT
SCATT MEMV BLANK
STORE MTCAT.CODCAT TO M.CODCAT
STORE MTSEC.CODSEC TO M.CODSEC
STORE MTPLA.CODPLA TO M.CODPLA
DO WHILE .T.
   SELECT MTACT
   STORE SPACE(LEN(M.DESACT1)) TO M.DESACT1
   STORE SPACE(LEN(M.DESACT2)) TO M.DESACT2
   STORE SPACE(LEN(M.DESACT3)) TO M.DESACT3
   STORE SPACE(LEN(M.DESACT4)) TO M.DESACT4
   STORE SPACE(LEN(M.DESACT5)) TO M.DESACT5
   DO GETACT
   IF LASTKEY()=27
      EXIT
   ENDIF
ENDDO
POP KEY
RETURN

************
PROC PROACTM
************
PUSH KEY CLEAR
STORE "MODIFICAR" TO WFLAGRACT
SELECT MTACT
IF .NOT. EOF()
   SCATT MEMV
   DO GETACT
ENDIF
POP KEY
RETURN

************
PROC PROACTE
************
SELECT MTACT
IF EOF()
   RETURN
ENDIF
PUSH KEY CLEAR
STORE "ELIMINAR" TO WFLAGRACT
STORE 2 TO WOP
DO CANCACEP WITH WFLAGRACT
IF WOP=2
   IF RECLOC()
      DELETE
      UNLOCK
   ELSE
      DO PROCNUL
   ENDIF
ENDIF
POP KEY
RETURN

***********
PROC GETACT
***********
DEFI WIND WINDGETA FROM 05,05 TO 16,75;
          TITLE  WFLAGRACT ;
          DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
ACTI WIND WINDGETA
DO WHILE .T.
   @ 00,01 SAY "Seccion .............:"
   @ 01,01 SAY "Plan ................:"
   @ 02,01 SAY "Tipo ................:"
   @ 03,01 SAY "Descripcion .........:"
   @ 08,01 SAY "Costo total .........:"

   IF WFLAGRACT="INCLUIR"
      IF ORDER()="MTACTXS"
          @ 0,24 SAY M.CODSEC
          @ 1,24 GET M.CODPLA VALID VALPLA()
          READ
          IF LASTKEY()=27
             EXIT
          ENDIF
          SELECT MTPLA
          SEEK MTCAT.CODCAT+M.CODPLA
          IF .NOT.FOUND()
             STORE "CODIGO DE PLAN NO EXISTE" TO WTEXT
             DO AVISO WITH WTEXT
             LOOP
          ENDIF
      ELSE
          @ 0,24 GET M.CODSEC VALID VALSEC()
          @ 1,24 SAY M.CODPLA
          READ
          IF LASTKEY()=27
             EXIT
          ENDIF
          SELECT MTSEC
          SEEK MTCAT.CODCAT+M.CODSEC
          IF .NOT.FOUND()
             STORE "CODIGO DE SECCION NO EXISTE" TO WTEXT
             DO AVISO WITH WTEXT
             LOOP
          ENDIF
      ENDIF
   ELSE
       @ 0,24 SAY M.CODSEC
       @ 1,24 SAY M.CODPLA
   ENDIF
   @ 2,24   GET M.TIPO    VALID VALTIP()
   @ 3,24   GET M.DESACT1 VALID VALDES()
   @ 4,24   GET M.DESACT2
   @ 5,24   GET M.DESACT3
   @ 6,24   GET M.DESACT4
   @ 7,24   GET M.DESACT5
   @ 8,24   GET M.COSTO PICTURE "99,999,999.99"
   READ
   IF LASTKEY()=27
      EXIT
   ENDIF
   STORE 1 TO WOP
   DO ACEPCANC WITH WFLAGRACT
   IF WOP=1
      IF WFLAGRACT="INCLUIR"
         SELECT MTCAT
         IF FILLOC()
            REPLACE NUMACT WITH STR((VAL(NUMACT)+1),10)
            UNLOCK ALL
            STORE NUMACT TO M.CODACT
         ELSE
           DO PROCNUL
         ENDIF
         SELECT MTACT
         IF FILLOC()
            APPEND BLANK
            GATH MEMV
            UNLOCK ALL
         ELSE
           DO PROCNUL
         ENDIF
      ELSE
         SELECT MTACT
         IF RECLOC()
            GATH MEMV
            UNLOCK ALL
         ELSE
            DO PROCNUL
         ENDIF
      ENDIF
   ENDIF
   EXIT
ENDDO
RELE WIND WINDGETA
RETURN

***********
FUNC VALDES
***********
IF M.DESACT1<>SPACE(LEN(M.DESACT1))
   RETURN .T.
ELSE
   RETURN .F.
ENDIF

***********
FUNC VALPLA
***********
IF M.CODPLA=SPACE(LEN(M.CODPLA))
   DO MTAPLA
   STORE CODPLA TO M.CODPLA
   STORE .T. TO WRETURN
ELSE
   SELECT MTPLA
   SEEK MTCAT.CODCAT+M.CODPLA
   IF FOUND()
      STORE .T. TO WRETURN
   ELSE
      STORE .F. TO WRETURN
   ENDIF
   SELECT MTACT
   RETURN WRETURN
ENDIF

***********
FUNC VALSEC
***********
IF M.CODSEC=SPACE(LEN(M.CODSEC))
   DO MTASEC
   STORE CODSEC TO M.CODSEC
   STORE .T. TO WRETURN
ELSE
   SELECT MTSEC
   SEEK MTCAT.CODCAT+M.CODSEC
   IF FOUND()
      STORE .T. TO WRETURN
   ELSE
      STORE .F. TO WRETURN
   ENDIF
   SELECT MTACT
   RETURN WRETURN
ENDIF

***********
FUNC VALTIP
***********
SELECT MTTACT
SEEK M.TIPO
IF FOUND()
   IF M.DESACT1=SPACE(LEN(M.DESACT1))
      STORE DESTIP  TO M.DESACT1
   ENDIF
   STORE .T.     TO WRETURN
ELSE
   DO MTATACT
   STORE CODTIP  TO M.TIPO
   IF M.DESACT1=SPACE(LEN(M.DESACT1))
      STORE DESTIP  TO M.DESACT1
   ENDIF
   STORE .T.     TO WRETURN
ENDIF
SELECT MTACT
RETURN WRETURN

************
FUNC FUNDESP
************
SELECT MTPLA
SEEK MTACT.CODCAT+MTACT.CODPLA
IF FOUND()
   STORE DESPLA TO WDESPLA
ELSE
   STORE "?"    TO WDESPLA
ENDIF
SELECT MTACT
RETURN WDESPLA

************
FUNC FUNDESS
************
SELECT MTSEC
SEEK MTACT.CODCAT+MTACT.CODSEC
IF FOUND()
   STORE DESSEC TO WDESSEC
ELSE
   STORE "?"    TO WDESSEC
ENDIF
SELECT MTACT
RETURN WDESSEC

************
FUNC FUNDEST
************
SELECT MTTACT
SEEK MTACT.TIPO
IF FOUND()
   STORE DESTIP TO WDESTIP
ELSE
   STORE "?"    TO WDESTIP
ENDIF
SELECT MTACT
RETURN WDESTIP
